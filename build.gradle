import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.Dockerfile

plugins {
    id 'org.springframework.boot' version '2.5.5'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'com.bmuschko.docker-remote-api' version '7.1.0'
}

group = 'kz.hawk'
version = '0.0.1'
sourceCompatibility = '11'
def imageName = "hawk6884/$project.name:$project.version"

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {

    // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-jdbc
    implementation 'org.springframework.boot:spring-boot-starter-jdbc:2.5.5'

    // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-web
    implementation 'org.springframework.boot:spring-boot-starter-web:2.5.5'

    // https://mvnrepository.com/artifact/org.liquibase/liquibase-core
    implementation 'org.liquibase:liquibase-core:4.4.3'

    // https://mvnrepository.com/artifact/org.mybatis.spring.boot/mybatis-spring-boot-starter
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:2.2.0'

    // https://mvnrepository.com/artifact/org.mybatis.spring.boot/mybatis-spring-boot-starter-test
    testImplementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter-test:2.2.0'


    // https://mvnrepository.com/artifact/org.postgresql/postgresql
    runtimeOnly 'org.postgresql:postgresql:42.2.24'

    // https://mvnrepository.com/artifact/org.projectlombok/lombok
    compileOnly 'org.projectlombok:lombok:1.18.20'
    testCompileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.20'

    // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-test
    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.5.5'

    // https://mvnrepository.com/artifact/kz.greetgo.conf/greetgo.conf
    implementation 'kz.greetgo.conf:greetgo.conf:2.0.0'

    // https://mvnrepository.com/artifact/com.squareup.okhttp3/okhttp
    implementation 'com.squareup.okhttp3:okhttp:4.9.2'

    // https://mvnrepository.com/artifact/org.testng/testng
    testImplementation 'org.testng:testng:7.4.0'

    // https://mvnrepository.com/artifact/javax.validation/validation-api
    implementation 'javax.validation:validation-api:2.0.1.Final'

    // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-validation
    implementation 'org.springframework.boot:spring-boot-starter-validation:2.5.5'

    // https://mvnrepository.com/artifact/kz.greetgo/greetgo.scheduling
    implementation 'kz.greetgo:greetgo.scheduling:3.0.3'

}

test {
    useTestNG()
    testLogging.exceptionFormat = "full"
}


task createDockerFile(type: Dockerfile) {
    destFile = file('build/docker/Dockerfile');
    from("openjdk:11.0-jre")
    copyFile("${project.name}-${project.version}.jar", "app.jar")
    entryPoint("java", "-jar", "/app.jar")
}

task copyJar(type: Copy) {
    dependsOn bootJar, createDockerFile
    from('build/libs') { include '*.jar' }
    into('build/docker')
}

task createDockerImage(type: DockerBuildImage) {
    dependsOn copyJar
    inputDir = file('build/docker')
    images = [imageName]
    noCache = true
}
